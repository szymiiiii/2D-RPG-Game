class_name StateMachine extends Node

@export var initial_state: State = null

@onready var state: State = (func get_initial_state() -> State:
	if DoorManager.did_player_go_through_doors:
		return get_child(1) #Getting out
	if initial_state != null :
		return initial_state 
	else:
		return get_child(0) #idle
).call()


func _ready() -> void:
	for state_node: State in find_children("*", "State"):
		state_node.finished.connect(_transition_to_next_state)

	await owner.ready
	state.enter("")


func _unhandled_input(event: InputEvent) -> void:
	state.handle_input(event)


func _process(delta: float) -> void:
	state.update(delta)


func _physics_process(delta: float) -> void:
	state.physics_update(delta)


func _transition_to_next_state(target_state_path: String, data: Dictionary = {}) -> void:
	if not has_node(target_state_path):
		printerr(owner.name + ": Trying to transition to state " + target_state_path + " but it does not exist.")
		return

	var previous_state_path := state.name
	#print("stan ", previous_state_path, " przechodzi w ", target_state_path)
	state.exit()
	state = get_node(target_state_path)
	state.enter(previous_state_path, data)
